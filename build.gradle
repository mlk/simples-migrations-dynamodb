buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: "info.solidsoft.pitest"
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'com.github.mlk'
version = "1.0.${System.env.CIRCLE_BUILD_NUM}"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

task copyNativeDeps(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into 'build/libs'
}

test {
    dependsOn copyNativeDeps
    systemProperty "java.library.path", 'build/libs'
}

dependencies {
    compile 'com.github.mlk:simples-migrations:1.0.2'
    compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.125'

    compileOnly 'org.projectlombok:lombok:1.16.16'

    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.3.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-web:1.5.3.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-actuator:1.5.3.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-undertow:1.5.3.RELEASE'
    testCompile 'org.apache.httpcomponents:httpclient:4.5.3'

    testCompile 'com.amazonaws:DynamoDBLocal:1.11.0.1'
    testCompile 'com.github.mlk:assortmentofjunitrules:1.5.32'
}

uploadArchives {
    if(project.hasProperty("ossrhUsername")) {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'simples-migrations-dynamodb'
                    packaging 'jar'
                    description 'A wrapper around a wrapper around a wrapper around something that does sftp'
                    url 'https://www.github.com/mlk/simples-migrations-dynamodb'

                    scm {
                        connection 'scm:git:https://www.github.com/mlk/simples-migrations-dynamodb/'
                        url 'https://www.github.com/mlk/simples-migrations-dynamodb/'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://www.github.com/mlk/simples-migrations-dynamodb/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'mlk'
                            name 'Michael Lee'
                            email 'pom@michael-lloyd-lee.me.uk'
                        }
                    }
                }
            }
        }
    }
}
